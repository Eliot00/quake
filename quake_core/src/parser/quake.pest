earth = {
    action_decl
    | transflow_decl
    | layout_decl
}

action_decl = {
    object ~ "." ~ action ~ parameters? ~ ":"? ~ " "* ~ text?
}

transflow_decl = {
    "transflow" ~ ident ~ "{" ~ (transflow_expr ~ ("," ~ transflow_expr)? ~ ";"?)? ~ "}"
}

transflow_expr = {
    midway | endway
}

midway = {
    from ~ parameters ~ "." ~ to ~ lbracket ~ parameter ~ rbracket ~ filter_expr?
}

endway = {
    from ~ parameters ~ "." ~ to ~ lbracket ~ component_decl ~ rbracket ~ filter_expr?
}

filter_expr = {
    "." ~ filter_str ~ lbracket ~ (double_quoted_string |single_quoted_string) ~ rbracket
}

filter_str = { "filter" | "FILTER" }

component_decl = {
    "<" ~ component_name ~ ">"
}

component_name = {
    ident
}

from = { "from" }
to = { "to" }

parameters = {
    lbracket ~ parameter ~ ("," ~ parameter)* ~ rbracket
}

lbracket = {
    "(" | "（"
}

rbracket = {
    ")" | "）"
}

layout_decl = {
    "layout" ~ ident ~ "{" ~ flex_child* ~ "}"
}

flex_child = {
    "-" ~ "-"*
    | ("|" ~ component_use_decl)  ~  ("|"  ~  component_use_decl)*  ~  "|"
}

component_use_decl =  {
    sized_empty_comp
  | use_name ~ lbracket ~ call_flow ~  rbracket
}

sized_empty_comp = {
    ("EMPTY" | "empty" | "Empty") ~ lbracket ~ digits ~  ("x" | "X") ~ rbracket
}

call_flow = {
    "flow" ~ lbracket ~ string ~ rbracket ~ ("," ~ digits ~ ("x" | "X"))?
}

use_name = { ident }

parameter = {
    digits | single_quoted_string | double_quoted_string
}

digits =  {
   digit+
}

string = @{
    double_quoted_string |
    single_quoted_string
}

double_quoted_string  = @{ "\"" ~ (!("\"") ~ ANY)* ~ "\""}
single_quoted_string  = @{ "\'" ~ (!("\'") ~ ANY)* ~ "\'"}

object = { ident }

action = { ident }

text = { ANY* }

ident = { (alpha | digit)+ }

alpha = { 'a'..'z' | 'A'..'Z' | "-" | "_" }
digit = { '0'..'9' }

WHITESPACE = _{ " " | "\t" | NEWLINE }

